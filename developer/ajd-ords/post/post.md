# Submit Form Data using ORDS
    
## Introduction
    
The POST method is similar. This method is used by the client to communicate with the server when asking for a response that takes into account the data provided in the body of the HTTP request. If a form is sent using this method, the data is appended to the body of the HTTP request.
    
Oracle RESTful Data Services supports POST as the HTTP method. In POST method, input parameters are encoded in the payload and output parameters are decoded from the response. You can use JSON in the body of REST requests, such as the POST or PUT method, where each parameter is a JSON name/value pair.

Estimated Lab Time: 15 minutes
    
## Task 1: Create table and new module for POST
    
1. Open SQL Developer Web on Tools tab, and login to AJD as DEMO user.
    
    https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/_sdw/?nav=worksheet
    
    - Username: demo
    - Password: DBlearnPTS#20_
    
2. Create a new table on AJD instance that will store orders submitted from ADW instance via REST APIs calls.
    
    ````
    CREATE TABLE "ORDERS"
    ("ID" NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY MINVALUE 1 MAXVALUE 9999999999999999999999999999 INCREMENT BY 1 START WITH 1 CACHE 20 NOT NULL ENABLE,
    "CUSTOMER_NAME" VARCHAR2(255) NOT NULL ENABLE,
    "PRODUCT_ID" VARCHAR2(255) NOT NULL ENABLE,
    "QUANTITY" NUMBER NOT NULL ENABLE,
    CONSTRAINT "ORDERS_ID_PK" PRIMARY KEY ("ID")
    );
    ````
    
3. Verify the table was created.
    
    ````
    select * from ORDERS;
    
4. No items to display.
    ````
    
5. Login to Apex environment using DEMO workspace on AJD:
    
    https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/f?p=4500:1000
    
    - Workspace: demo
    - Username: demo
    - Pasword: DBlearnPTS#20_
    
6. **Sign In**.
    
7. Access SQL Workshop > **RESTful Services**.
    
8. For submitting orders, as it is a new functionality for a new collection of our Pet Shop application, we will create a new template under the same **demo.auth-catfood** module.
    
9. Select Modules > demo.auth-catfood. Click **Create Template**.
    
    - URI Template: orders/
    
10. **Create Template**.
    
    The main difference of this feature is at handler level. This time we don't have a GET method with a SELECT statement, but a POST method with an INSERT statement.
    
11. Click **Create Handler**.
    
    - Method: POST
    - Source:
    
    ````
    BEGIN
        insert into ORDERS (CUSTOMER_NAME, PRODUCT_ID, QUANTITY) values (:cusn, :prod, :qtty);
    EXCEPTION
        when others then
            :status := 400;
    END;
    ````
    
12. This handler needs 4 parameters: 3 inputs and 1 output. Some may argue that a web service can have only one input parameter with JSON format, and multiple fields. It is true that we could use a JSON document with three fields in this case as an input, but I want to show also this case, with individual parameters, data types, and values. For the output, one parameter is enough for a simple case, returning the status, so we can verify if the submit was a success or a failure.
    
13. Under Parameters, click **Add Row**.
    
    - Name: CUSN
    - Bind Variable: cusn
    - Access Method: IN
    - Source Type: HTTP HEADER
    - Data Type: STRING
    
14. Under Parameters, click **Add Row**.
    
    - Name: PROD
    - Bind Variable: prod
    - Access Method: IN
    - Source Type: HTTP HEADER
    - Data Type: STRING
    
15. Under Parameters, click **Add Row**.
    
    - Name: QTTY
    - Bind Variable: qtty
    - Access Method: IN
    - Source Type: HTTP HEADER
    - Data Type: INTEGER
    
16. Under Parameters, click **Add Row**.
    
    - Name: X-APEX-STATUS-CODE
    - Bind Variable: status
    - Access Method: OUT
    - Source Type: HTTP HEADER
    - Data Type: INTEGER
    
17. Click **Create Handler**.
    
18. For the test, we will need the URL of this new web service.
    
19. Copy Full URL value https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/auth-cats/orders/ in your notes.
    
## Task 2: Use POST method to submit orders
    
1. Let's submit some orders from ADW into AJD using this new POST method. Open SQL Developer Web on Tools tab, and login to ADW as ADMIN user.
    
    https://kndl0dsxmmt29t1-vltadw.adb.eu-frankfurt-1.oraclecloudapps.com/ords/admin/_sdw/?nav=worksheet
    
    - Username: admin
    - Password: DBlearnPTS#20_
    
2. Try to insert an order with NO authentication.
    
    ````
    DECLARE
      vcRequestBody clob;
    BEGIN
      apex_web_service.g_request_headers(1).name := 'Content-Type';
      apex_web_service.g_request_headers(1).value := 'application/x-www-form-urlencoded'; 
      vcRequestBody := apex_web_service.make_rest_request(
            p_url => 'https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/auth-cats/orders/',
            p_http_method => 'POST',
            p_parm_name => apex_util.string_to_table('cusn:prod:qtty'),
            p_parm_value => apex_util.string_to_table('Leo Rodriguez:64D44979B6EC4F55BF7DDA60BD485E03:3'));
    END;
    ````
    
3. Open SQL Developer Web on Tools tab, and login to AJD as DEMO user. You can use the saved link, opening it on a new web browser tab. At this point it is good to have both SQL Developer Web connections in two tabs in your browser: ADMIN on ADW and DEMO on AJD.
    
    https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/_sdw/?nav=worksheet
    
    - Username: demo
    - Password: DBlearnPTS#20_
    
4. Verify the order you submitted from ADW was saved correctly in AJD.
    
    ````
    select * from ORDERS;
    ````
    
    | ID | CUSTOMER_NAME | PRODUCT_ID                       | QUANTITY |
    |:---|:--------------|:---------------------------------|:---------|
    |  1 | Leo Rodriguez | 64D44979B6EC4F55BF7DDA60BD485E03 |        3 |

5. When we submitted the order from ADW to AJD, the procedure completed successfully, but this doesn't mean the order was received and saved successfully. Some information about the request can be retrieved from the request response headers.
    
    ````
    DECLARE
      vcRequestBody clob;
    BEGIN
      apex_web_service.g_request_headers(1).name := 'Content-Type';
      apex_web_service.g_request_headers(1).value := 'application/x-www-form-urlencoded'; 
      vcRequestBody := apex_web_service.make_rest_request(
            p_url => 'https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/auth-cats/orders/',
            p_http_method => 'POST',
            p_parm_name => apex_util.string_to_table('cusn:prod:qtty'),
            p_parm_value => apex_util.string_to_table('Ana Maria:4178FDB13CCE4F55BF4A16F240AE0333:2'));
    
      for i in 1.. apex_web_service.g_headers.count loop 
        dbms_output.put_line('Response header name: ' || apex_web_service.g_headers(i).name || 
          ', response header value: ' || apex_web_service.g_headers(i).value);
      end loop;
    END;
    ````
    
6. In our case, we are interested in the request status code, stored in the global `G_STATUS_CODE`. A 200 status code means the request has succeeded.
    
    ````
    DECLARE
      vcRequestBody clob;
    BEGIN
      apex_web_service.g_request_headers(1).name := 'Content-Type';
      apex_web_service.g_request_headers(1).value := 'application/x-www-form-urlencoded'; 
      vcRequestBody := apex_web_service.make_rest_request(
            p_url => 'https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/auth-cats/orders/',
            p_http_method => 'POST',
            p_parm_name => apex_util.string_to_table('cusn:prod:qtty'),
            p_parm_value => apex_util.string_to_table('Javier Roca:85351E81C2314FFEBF518C73DFC65A0B:10'));
    
      if apex_web_service.g_status_code = 200 then -- OK
        dbms_output.put_line('Web service invoked successfully');
      end if;
    END;
    ````
    
## Task 3: Secure POST method with authentication
    
1. Add authentication to the new web service to secure this POST method.
    
2. Login to Apex environment using DEMO workspace on AJD:
    
    https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/f?p=4500:1000
    
    - Workspace: demo
    - Username: demo
    - Pasword: DBlearnPTS#20_
    
3. **Sign In**.
    
4. Navigate to SQL Workshop > RESTful Services. Select Modules > demo.auth-catfood. Review Resource Templates table:
    
    | URI Template	| Protected by Privilege |
    |:-------------|:----------|
    |      orders/ | Template not protected by any privilege |
    |     catfood/ | Template fully protected by a privilege |
    
5. This table shows that our new **orders/** template is not protected by any privilege. Open SQL Developer Web on Tools tab, and login to AJD as DEMO user.
    
    https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/_sdw/?nav=worksheet
    
    - Username: demo
    - Password: DBlearnPTS#20_
    
6. Create a new privilege mapping for this template, so users can no longer access the web service without authentication. We will use the existing **demo.auth-catfood-priv** privilege.
    
    ````
    BEGIN
     ords.create_privilege_mapping(
          p_privilege_name => 'demo.auth-catfood-priv',
          p_pattern => '/auth-cats/orders/*');     
      commit;
    END;
    ````
    
7. Now we test the authentication from ADW side, which is the client in our case. Open SQL Developer Web on Tools tab, and login to ADW as ADMIN user.
    
    https://kndl0dsxmmt29t1-vltadw.adb.eu-frankfurt-1.oraclecloudapps.com/ords/admin/_sdw/?nav=worksheet
    
    - Username: admin
    - Password: DBlearnPTS#20_
    
8. First, try to submit an order with NO authentication. Even the procedure finishes with no errors, the response we receive from the web service is not status 200, meaning our request has failed. You query the `ORDERS` table on AJD, and confirm this order was not received.
    
    ````
    DECLARE
      vcRequestBody clob;
    BEGIN
      apex_web_service.g_request_headers(1).name := 'Content-Type';
      apex_web_service.g_request_headers(1).value := 'application/x-www-form-urlencoded'; 
      vcRequestBody := apex_web_service.make_rest_request(
            p_url => 'https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/auth-cats/orders/',
            p_http_method => 'POST',
            p_parm_name => apex_util.string_to_table('cusn:prod:qtty'),
            p_parm_value => apex_util.string_to_table('Luisa Martinez:B597E9A0A52E4F2FBFDBEA215E92651B:2'));
    
      if apex_web_service.g_status_code = 200 then -- OK
        dbms_output.put_line('Web service invoked successfully');
      else -- NOT OK
        dbms_output.put_line('Error invoking web service');
      end if;
    END;
    
    Error invoking web service
    ````
    
9. Add the authentication lines of code to this procedure, and run it again on ADW. This time the order is received successfully on AJD, and stored in the `ORDERS` table.
    
    ````
    DECLARE
      vcRequestBody clob;
      vcToken varchar2(255);
    BEGIN
      apex_web_service.oauth_authenticate(p_token_url => 'https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/oauth/token',
                                         p_client_id => 'S3t0Of0Ss98_6RWZFfTWjA..',
                                         p_client_secret => 'R54mUH7ovnTWbTmA3CqKrA..',
                                         p_wallet_path => '');
      vcToken:= apex_web_service.oauth_get_last_token;
      
      apex_web_service.g_request_headers(1).name := 'Content-Type';
      apex_web_service.g_request_headers(1).value := 'application/x-www-form-urlencoded'; 
      
      apex_web_service.g_request_headers(2).name := 'Authorization';
      apex_web_service.g_request_headers(2).value := 'Bearer ' || vcToken;
      
      apex_web_service.oauth_set_token(p_token => vcToken);
      
      vcRequestBody := apex_web_service.make_rest_request(
            p_url => 'https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/auth-cats/orders/',
            p_http_method => 'POST',
            p_parm_name => apex_util.string_to_table('cusn:prod:qtty'),
            p_parm_value => apex_util.string_to_table('Luisa Martinez:B597E9A0A52E4F2FBFDBEA215E92651B:2'));
      
      if apex_web_service.g_status_code = 200 then -- OK
        dbms_output.put_line('Web service invoked successfully');
      else -- NOT OK
        dbms_output.put_line('Error invoking web service');
      end if;
    END;
    
    Web service invoked successfully
    ````
    
10. Verify all orders are stored on AJD. Open SQL Developer Web on Tools tab, and login to AJD as DEMO user.
    
    https://kndl0dsxmmt29t1-vltajd.adb.eu-frankfurt-1.oraclecloudapps.com/ords/demo/_sdw/?nav=worksheet
    
    - Username: demo
    - Password: DBlearnPTS#20_
    
11. List all orders. The information we submitted was structured relational data in individual fields.
    
    ````
    select * from ORDERS;
    ````
    
    | ID | CUSTOMER_NAME | PRODUCT_ID | QUANTITY |
    |:----------|:----------|:----------|:----------|
    | 1 | Leo Rodriguez | 64D44979B6EC4F55BF7DDA60BD485E03 | 3 |
    | 2 | Ana Maria | 4178FDB13CCE4F55BF4A16F240AE0333 | 2 |
    | 3 | Javier Roca | 85351E81C2314FFEBF518C73DFC65A0B | 10 |
    | 4 | Luisa Martinez | B597E9A0A52E4F2FBFDBEA215E92651B | 2 |
    
12. You can use now the same flow to create a new module that submits semi-structured data in JSON documents, from ADW to AJD, secured with authentication.
    
## Acknowledgements
    
- **Author** - Valentin Leonard Tabacaru
- **Last Updated By/Date** - Valentin Leonard Tabacaru, Principal Product Manager, DB Product Management, Sep 2020
